[FormDesignerId("E666FD5B-2B21-4F24-995B-0E94598A1797")]
[ClassId("DF51CB3C-7A6C-40E2-A105-E934B1F5EC87")]
[InterfaceId("0E762084-D46B-46CC-B427-CCA2F3ECC785")]
[EventInterfaceId("ABD0FE2E-8008-465C-9FDF-88C24A597651")]
Class frmMain
    Attribute VB_Name = "frmMain"
    Attribute VB_GlobalNameSpace = False
    Attribute VB_Creatable = False
    Attribute VB_PredeclaredId = True
    Attribute VB_Exposed = False
    ' //
    ' // Direct2D blur demo by The trick
    ' //
    
    Option Explicit
    
    Private Const GENERIC_READ As Long = &H80000000
    
    Private m_cFactory      As ID2D1Factory
    Private m_cWICFactory   As WICImagingFactory
    Private m_cRenderTarget As ID2D1DeviceContext
    Private m_cEffect       As ID2D1Effect
    
    Private Sub Form_Load()
        Dim cBitmap     As ID2D1Bitmap
        Dim cHwndTgt    As ID2D1HwndRenderTarget
    
        ' // Create a factory
        Set m_cFactory = D2D1.CreateFactory()
    
        ' // Create render target
        Set m_cRenderTarget = m_cFactory.CreateHwndRenderTarget(D2D1.RenderTargetProperties(D2D1.PixelFormat()), _
                                                                D2D1.HwndRenderTargetProperties(picResult.hWnd, D2D1.SizeU()))
        
        Set cHwndTgt = m_cRenderTarget
        
        cHwndTgt.Resize D2D1.SizeU(picResult.ScaleWidth, picResult.ScaleHeight)
    
        Set cBitmap = LoadImage(App.Path & "\image_01.jpg", m_cRenderTarget)
    
        ' // Create blur effect
        Set m_cEffect = m_cRenderTarget.CreateEffect(CLSID_D2D1GaussianBlur)
    
        m_cEffect.SetInput 0, cBitmap, 1
        m_cEffect.SetValue D2D1_GAUSSIANBLUR_PROP_BORDER_MODE, D2D1_PROPERTY_TYPE_UNKNOWN, D2D1_BORDER_MODE_HARD, 4
        
        sldValue_Change
        
    End Sub
    
    Private Function LoadImage( _
                     ByRef sPath As String, _
                     ByVal cRenderTarget As ID2D1RenderTarget) As ID2D1Bitmap
        Dim cDecoder        As IWICBitmapDecoder
        Dim cFrameDecode    As IWICBitmapFrameDecode
        Dim cConverter      As IWICFormatConverter
        
        If m_cWICFactory Is Nothing Then
            Set m_cWICFactory = New WICImagingFactory
        End If
        
        Set cDecoder = m_cWICFactory.CreateDecoderFromFilename(StrPtr(sPath), vbNullPtr, GENERIC_READ, WICDecodeMetadataCacheOnLoad)
        Set cFrameDecode = cDecoder.GetFrame(0)
        Set cConverter = m_cWICFactory.CreateFormatConverter()
        
        cConverter.Initialize cFrameDecode, GUID_WICPixelFormat32bppPBGRA, WICBitmapDitherTypeNone, Nothing, 0, WICBitmapPaletteTypeMedianCut
        
        Set LoadImage = cRenderTarget.CreateBitmapFromWicBitmap(ByVal cConverter, vbNullPtr)
        
    End Function
    
    Private Sub picResult_Paint()
    
        m_cRenderTarget.BeginDraw
        
        m_cRenderTarget.Clear D2D1.ColorF(Indigo)
    
        m_cRenderTarget.DrawImage m_cEffect, vbNullPtr, vbNullPtr, 1, 0
        
        m_cRenderTarget.EndDraw ByVal 0, ByVal 0
        
    End Sub
    
    Private Sub sldValue_Change()
        m_cEffect.SetValue D2D1_GAUSSIANBLUR_PROP_STANDARD_DEVIATION, D2D1_PROPERTY_TYPE_FLOAT, CSng(sldValue.Value / 10), 4
        picResult_Paint
    End Sub
    
    Private Sub sldValue_Scroll()
        sldValue_Change
    End Sub
    

End Class
